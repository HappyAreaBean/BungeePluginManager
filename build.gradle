import java.nio.file.Files
import java.nio.file.StandardCopyOption

plugins {
	id 'com.github.johnrengelman.shadow' version '5.0.0'
	id 'java'
}


defaultTasks 'build'


group 'bungeepluginmanager'
version '2.1'
sourceCompatibility = JavaVersion.VERSION_1_8


File targetJarDirectory = new File(project.rootDir, "target")


tasks.register('setupFolders') {
	doLast {
		targetJarDirectory.mkdirs()
	}
}


sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
		resources {
			srcDirs = ['resources']
		}
	}
}


repositories {
	mavenCentral()
	maven {
		url 'https://oss.sonatype.org/content/repositories/snapshots'
	}
}


dependencies {
	compileOnly group: 'net.md-5', name: 'bungeecord-api', version: '1.16-R0.4'
	compileOnly group: 'org.yaml', name: 'snakeyaml', version: '1.26'
}

compileJava {
	options.encoding = 'UTF-8'
	options.incremental = false
}


shadowJar {
	doFirst {
		archiveFile.get().getAsFile().delete()
	}

	from sourceSets.main.java.srcDirs
	from 'LICENSE'

	setArchivesBaseName(jar.getArchiveBaseName().get())
}

tasks.processResources {
	expand("pluginVersion": project.version)
}

tasks.register('copyFinalJarToTarget') {
	doLast {
		Files.copy(
			shadowJar.getArchiveFile().get().getAsFile().getAbsoluteFile().toPath(),
			new File(targetJarDirectory, jar.archiveBaseName.get() + '.jar').getAbsoluteFile().toPath(),
			StandardCopyOption.REPLACE_EXISTING
		)
	}
}


compileJava.dependsOn(clean)
compileJava.dependsOn(setupFolders)
jar.enabled = false
jar.finalizedBy(shadowJar)
shadowJar.finalizedBy(copyFinalJarToTarget)
